import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
}

def pluginName = 'LuckyEssentials'
group 'id.luckynetwork.dev.lyrams.lej'
version '0.7.1-RELEASE'

repositories {
    mavenCentral()
    mavenLocal()

    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url = "https://s01.oss.sonatype.org/content/repositories/snapshots/" }
}

configurations {
    extraLibs
}

dependencies {
    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    compileOnly 'org.jetbrains:annotations:23.0.0'
    compileOnly 'org.projectlombok:lombok:1.18.24'
    compileOnly 'org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT'

    extraLibs 'id.luckynetwork.lyrams.lyralibs:core:b312'
    extraLibs 'id.luckynetwork.lyrams.lyralibs:bukkit:b299'

    extraLibs project(":version-support")
    extraLibs project(":version-support1_8")
    extraLibs project(":version-support1_12")
    extraLibs project(":version-support1_13")
    extraLibs project(":version-support1_16")
    extraLibs project(":version-support1_17")
    extraLibs project(":version-support1_18")
    extraLibs project(":version-support1_19")
    configurations.compileOnly.extendsFrom(configurations.extraLibs)
}

compileJava.options.encoding = 'UTF-8'

jar {
    archiveFileName = pluginName + "-" + project.version + ".jar"

    from {
        configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

processResources {
    filter ReplaceTokens, tokens: [
            "version"  : project.version.toString(),
            "mainClass": project.group + ".$pluginName",
            "name"     : pluginName
    ]
}

java.sourceCompatibility = JavaVersion.VERSION_1_8